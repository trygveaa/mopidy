# Maintainer: Alexandre Petitjean <alpetitjean at gmail dot com>

pkgname=mopidy
_pypiname=Mopidy
pkgver=0.19.4
pkgrel=1
pkgdesc="Mopidy is a music server which can play music both from multiple sources, like your local hard drive, radio streams, and from Spotify and SoundCloud."
arch=('any')
url="http://www.mopidy.com"
license=('APACHE')
depends=('python2'
         'python2-pykka'
         'python2-setuptools'
         'gstreamer0.10-python'
         'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins'
         'gstreamer0.10-ugly-plugins'
         'python2-tornado')
optdepends=('mopidy-spotify: Spotify backend'
            'mopidy-scrobbler: Last.FM scrobbler'
            'mopidy-MPRIS: MPRIS frontend')
conflicts=('mopidy-git')
backup=('etc/mopidy/mopidy.conf')
install=$pkgname.install
source=("https://pypi.python.org/packages/source/M/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        'man-mopidy.1.gz'
        'mopidy.conf'
        'mopidy.desktop'
        'mopidy.install'
        'mopidy.service')
md5sums=('3c8ade914ee19105e82d91bafad7964a'
         '55630327372a2136c7603b9b38beb690'
         'f198485317cd1f481b9b9a369e122833'
         'f6f94b7fb57d40dadbbfa93997d1b7ab'
         '3894a1c01f315e32b4fa954bf8cdaa1a'
         'f7f94cafab8b8ed381f06afb5a74533c')

package() {
  cd "${srcdir}/${_pypiname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm640 "${srcdir}/mopidy.conf" "${pkgdir}/etc/mopidy/mopidy.conf"
  install -Dm644 "${srcdir}/mopidy.service" "${pkgdir}/usr/lib/systemd/system/mopidy.service"
  install -Dm640 "${srcdir}/man-mopidy.1.gz" "${pkgdir}/usr/share/man/man1/mopidy.1.gz"
  install -Dm640 "${srcdir}/mopidy.desktop" "${pkgdir}/usr/share/applications/mopidy.desktop"

  install -dm755 "${pkgdir}/etc/mopidy/extensions.d"
  install -dm755 "${pkgdir}/var/cache/mopidy"
  install -dm755 "${pkgdir}/var/lib/mopidy/local"
  install -dm755 "${pkgdir}/var/lib/mopidy/media"
  install -dm755 "${pkgdir}/var/lib/mopidy/playlists"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
